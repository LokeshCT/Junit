/*
 * Define and register a task/artifact for building a JAR from XSDs using XMLBeans
 * Requires that the including build script defines the following *before* importing this script
 *  + schemaPath - path to the XSD files e.g. "$projectDir/schema/"
 *  + [inclusionPattern] - optional string, defaults to '*.xsd'
 */
dependencies {
    compile 'org.apache.xmlbeans:xmlbeans:2.5.0'
    compile 'net.sourceforge.saxon:saxon:9.1.0.8'
    compile 'net.sf.saxon:saxon-dom:9.0'
    compile ('org.apache.xmlbeans:xmlbeans-xpath:2.5.0') {
        exclude module:'saxon', group:'net.sf.saxon'
    }
}

task classes(overwrite: true) {
    description = "Build the XMLBeans classes from schema for ${project.name}"
    inputs.dir project.ext.schemaPath
    outputs.dir "${project.buildDir}/classes/main"
    doLast {
        ant.taskdef(name: 'xmlbean', classname: 'org.apache.xmlbeans.impl.tool.XMLBean', classpath: configurations.compile.asPath)
        ant.xmlbean(srcgendir: "${project.buildDir}/generated-sources/xmlbeans",
                    classgendir: "${project.buildDir}/classes/main",
                    javasource: '1.5',
                    failonerror: true,
                    includeAntRuntime: false,
                    classpath: project.configurations.compile.asPath) {
            fileset(dir: schemaPath, includes: project.ext.has('inclusionPattern') ? project.ext.inclusionPattern : '*.xsd')
        }
    }
}


clean.onlyIf { project.hasProperty('forceClean') }
